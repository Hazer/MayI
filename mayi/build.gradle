apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'

group = 'com.thanosfisherman.mayi'
version = rootProject.ext.publishVersionName

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.publishVersionCode
        versionName rootProject.ext.publishVersionName

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        encoding "UTF-8"
        sourceCompatibility rootProject.ext.sourceCompatibilityVersion
        targetCompatibility rootProject.ext.targetCompatibilityVersion
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile "com.android.support:appcompat-v7:$rootProject.ext.supportLibrariesVersion"
    compile "com.android.support:support-annotations:$rootProject.ext.supportLibrariesVersion"
    testCompile 'junit:junit:4.12'
}

if (project.rootProject.file("local.properties").exists()) {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())

    bintray {
        user = properties.getProperty("bintray.user")
        key = properties.getProperty("bintray.apikey")
        configurations = ['archives']
        dryRun = false
        publish = true

        pkg {
            repo = "maven"
            name = "mayi"
            desc = 'Yet another runtime permission library for Android'
            websiteUrl = 'https://github.com/ThanosFisherman/MayI'
            vcsUrl = 'https://github.com/ThanosFisherman/MayI.git'
            licenses = ["Apache-2.0"]
            version {
                name = rootProject.ext.publishVersionName
                desc = 'MayI current version'
                released = new Date()
                vcsTag = rootProject.ext.publishVersionName
                //attributes = ['gradle-plugin': 'com.use.less:com.use.less.gradle:gradle-useless-plugin']
            }
        }
    }
}
task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}